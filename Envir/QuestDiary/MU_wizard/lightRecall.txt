;;创天网络有限公司原版1.45
[@mugong_lightstick]
{
#IF
check [029] 1
#SAY
你还没有收到雷电术秘籍吗？哦，年轻人 {FCOLOR/10}健忘症{FCOLOR/12} 也太严重了嘛。\ \ 
<结束/@exit>
#ACT
break
;
#IF
check [051] 1
#SAY
真是很奇怪嘛 \
凭你的能力那里面好像没有你制服不了的怪物。。\
无论如何，再试一次吗？\ \
<好的，拜托您了。/@mugong_lightstick_next4_1>  \ 
<累积些经验，请以后再来！/@mugong_lightstick_next5>
#ACT
break
;
#IF
checkjob wizard
#ACT
goto @mugong_lightstick_next1
#ELSESAY
该武功不是其它职业的人很容易就熟练的武功，只有 {FCOLOR/10}魔法师{FCOLOR/12} 可以掌握。\ \ 
<结束/@exit>

[@mugong_lightstick_next1]
#IF
checkmagic 雷电术
#SAY
你好像已经修炼了 {FCOLOR/10}雷电术{FCOLOR/12} 。。。如果这样就没有向我接受训练的必要了。\ \
<结束/@exit>
#ELSEACT
goto @mugong_lightstick_next2

[@mugong_lightstick_next2]
#IF
checklevel 16
#SAY
雷电术是在敌人的头上 {FCOLOR/10}放射雷电，使他们受到惨重破坏的强大魔法{FCOLOR/12} 。一次只可以攻击一个物体，启动时间有些长，但是破坏力非常大。 如果没有抵抗闪电的能力，而受到该魔法的攻击。。嘿嘿嘿。。。\
由于雷电术要产生强大的雷电，开始训练的时候比学习任何魔法都要遇到很大的困难。尤其是开始面向天空发射轻微的闪电，转换为雷的过程是非常困难的。为了熟练掌握该魔法，应提高对 {FCOLOR/10}电击系列魔法{FCOLOR/12} 的理解力。\ \
<想学习雷电术。/@mugong_lightstick_next3>
#ELSESAY
雷电术是在敌人的头上 {FCOLOR/10}放射雷电，使他们受到惨重破坏的强大魔法{FCOLOR/12} 一次只可以攻击一个物体，启动时间有些长，但是破坏力非常大。\ 
但是凭你现在的实力好像还不能学习雷电术。经过一些训练后，再来吧！\ \ 
<结束/@exit>

[@mugong_lightstick_next3]
#IF
#ACT
SET [051] 1
#SAY
给你指出修炼雷电术的要点并不难，首先要看你是否具有学习雷电术的 {FCOLOR/10}资格{FCOLOR/12} 。如果不这样，学习威力强大魔法时走火入魔的危险会提高。\ 
怎么样？接受我的测试吗？\ \ 
<好的，我要试试。/@mugong_lightstick_next4_1> \
<现在好象还有些勉强。/@mugong_lightstick_next5>

[@mugong_lightstick_next5]
#IF
#SAY
嗯。。知道了。但是雷电术是魔法师的代表魔法，而且是一定要掌握的魔法。无论如何在最短的时日内掌握雷电术，对你的前途很有帮助。\ \
<结束/@exit>

[@mugong_lightstick_next4_1]
#IF
#SAY
现在要把你送到某一个地方。\ 
如果可以将那里 {FCOLOR/10}所有的怪物打败{FCOLOR/12} ，就认为你通过了该考验。需要记住的是那里所有的怪物都是可以进行电击魔法的怪物。通过和这种敌人的战斗，提高对电击魔法的理解是这个训练的目的。\ 
同时与怪物面对面，你可以掌握谁是首先要攻击的对象。希望你不要做任何不经过思考冲动、无意义的行动。\ 
我将你送到那儿的时间是5分钟。。\ 
5分钟过去后，你将重新回到这里。那就祝你走运！\ \ 
<移动/@mugong_lightstick_next4_2>

[@mugong_lightstick_next4_2]
#IF
checkhum 02_006 1
#SAY
已经有人在接受考验。。。请等一下。。\ \ 
<结束/@exit>
#ELSEACT
Monclear 02_006
TimeRecall 5
Param1 02_006
Param2 25
Param3 22
MonGen 僧侣僵尸61  1 1
MonGen 僵尸461  3 1
MonGen 掷斧骷髅61  2 1
mapmove 02_006 25  8
goto @leidianrenwu1
break




[@leidianrenwu1]
#ACT
mov A3 %USERMAP

#IF
EQUAL A3 "02_006"
#ACT
delaygoto [local] 10 @leidianrenwu1
#ELSEACT
BREAKTIMERECALL
break
}


[@mugong_lightstick_complete]
{
#IF
checkmagic 雷电术
#SAY
你好像已经修炼了 {FCOLOR/10}雷电术{FCOLOR/12} 。。。如果这样就没有向我接受训练的必要了。\ \
<结束/@exit>
#ACT
break
;
#IF
check [029] 1
#ACT
break
;
#IF
check [052] 0
#ACT
break
;
#IF
check [051] 0
#ACT
break
;
#IF
check [051] 1
check [052] 1
check [029] 0
#ACT
goto @mugong_lightstick_complete_next1
#ELSEACT
break

[@mugong_lightstick_complete_next1]
#IF
check [029] 0
#ACT
give 雷电术（秘籍） 1
give 闪电眼  1
give 金币 17000
reset [051] 1
SET [029] 1
#SAY
祝贺你 {FCOLOR/10}通过{FCOLOR/12} 了测试！我看你具有修炼雷电术的坚实基础。我已经为你写好了雷电术秘籍，参照练习吧。我再给你一些金币和东西，用在需要的地方。
嘿嘿，看见年轻人脸上充满成就感是老年人的最大快乐。修炼武功的过程中还会有困难的，请随时来找我。\ \ 
<结束/@exit>
}

[@mugong_lightstick_explain]
{
#IF
#SAY
如果想修炼雷电术，在一定的时间里将训练场内的所有怪物打败即可。\ \ 
<结束/@exit>
}
