;;创天网络有限公司原版1.45
[@mugong_masshiding_pre]
{
#IF
check [020] 1
#ACT
break

#IF
check [047] 1
#ACT
break

#IF
check [046] 1
#ACT
break

#IF
check [045] 1
check [046] 0
#SAY
还没有拜见清明子吗？\
去了以后问一下叫 {FCOLOR/10}小贩{FCOLOR/12} 男人的情况。\ \ 
<结束/@exit>
#IF
#ACT
goto @mugong_masshiding_pre1

[@mugong_masshiding_pre1]
#IF
checklevel 23
#ACT
goto @mugong_masshiding_pre2
#ELSEACT
break

[@mugong_masshiding_pre2]
#IF
checkmagic 集体隐身术
#ACT
break
#ELSEACT
goto @mugong_masshiding_pre3

[@mugong_masshiding_pre3]
#IF
check [045] 0
check [046] 0
#SAY
你看起来是非常有实力的道士哦。你知道有关 {FCOLOR/10}小贩{FCOLOR/12} 男子的故事吗？\ \
<没有听说过的名字.../@mugong_masshiding_pre4>
#ELSESAY
你看起来是非常有实力的道士哦。你知道有关 {FCOLOR/10}小贩{FCOLOR/12} 男子的故事吗？\ \
<结束/@exit>

[@mugong_masshiding_pre4]
#IF
#SAY
哦，不知道也是理所当然的。这个男人是不久之前逃到比奇省的伪道士，到处讲道士们的坏话。\ 
但是听了他的故事，他也很为难哟。\ \ 
<可以讲一讲他有什么事情吗？/@mugong_masshiding_pre5_2> \
<我很忙，就到此为止要走了。/@mugong_masshiding_pre5_1>

[@mugong_masshiding_pre5_1]
#IF
#SAY
好的，那么请走好。下次不要忘了多买些东西。\ \ 
<结束/@exit>

[@mugong_masshiding_pre5_2]
#IF
#SAY
他是边境城市出身的战士，和叫 {FCOLOR/10}成致{FCOLOR/12} 的道士关系非常好。某个时候他们为了和怪兽战斗而出去了，但是一次聚集了很多的怪兽，他们处于危险的境地。\ 
他们遇到了生死危机，那个道士偷偷隐藏自己的行踪不见了。那以后小贩总是批评道士们表里不一。\ 
我所知道的成致决不是那样虚伪的人呀。。。\ 
好像有什么误会。\ \ 
<嗯，很有趣儿，想多知道些儿。/@mugong_masshiding_pre6_2> \
<与我没有关系的事情哟。到此为止，我要走了。/@mugong_masshiding_pre6_1>

[@mugong_masshiding_pre6_1]
#IF
#SAY
我讲话看起来很乏味，请走好！\ \ 
<结束/@exit>

[@mugong_masshiding_pre6_2]
#IF
#ACT
set [045] 1
#SAY
对不起，更详细的内容我也不知道。\
如果真的想知道，拜见 {FCOLOR/10}清明子(道馆本馆,11:10){FCOLOR/12} 如何？? \ \
<结束/@exit>
}

[@mugong_masshiding]
{
#IF
check [020] 1
#SAY
你不是已经收到书吗？那为什么还想索要？\ \
<结束/@exit>
#ACT
break

#IF
checkmagic 集体隐身术
#SAY
那么困难学到的集体隐身术正在灵活地使用吧？\ \ 
<结束/@exit>
#ACT
break

#IF
check [046] 1
check [047] 0
#SAY
很遗憾没有找到任何东西哟。还有想再调查的想法吗？\ 
如果有想法，快点收集成致的 {FCOLOR/10}信息{FCOLOR/12} 。\
<结束/@exit>
#ACT
break

#IF
checklevel 23
#ACT
goto @mugong_masshiding0
#ELSESAY
集体隐身术。。如果对这样的武功感兴趣，好像需要再修炼些。\ \ 
<结束/@exit>

[@mugong_masshiding0]
#IF
check [045] 1
#ACT
goto @mugong_masshiding0_1
#ELSESAY
集体隐身术。。。\
集体隐身术和隐身术相同的是可以隐藏自己的动静，不同的是 {FCOLOR/10}集体隐身术可以隐藏包括你同事动静的魔法{FCOLOR/12} 。\ \ 
<结束/@exit>

[@mugong_masshiding0_1]
#IF
#SAY
呀，是你哟。今天有什么事情找我吗？\ \
<是的，今天是因为叫小贩人的事情。。。。/@mugong_masshiding1_2> \
<也就是为了问安和咨询而来的。/@mugong_masshiding1_1>

[@mugong_masshiding1_1]
#IF
#SAY
好的，我没有其它的事情。你也过得不错吧！\ \
<结束/@exit>

[@mugong_masshiding1_2]
#IF
#SAY
呀，知道那个故事吧。真是很焦急的事情。\ 
叫成致的人不是被判朋友的人，不知道是怎么回事儿。一定有不得已的缘由吧！\ 
实际上我也认为此事有些诧异，你听说了有关成致的其它事情吗? \ \ 
<我听说他找到了集体隐身术的武功书。/@mugong_masshiding2> \
<没有听说其它的事情。/@mugong_masshiding4>

[@mugong_masshiding2]
#IF
#SAY
什么叫集体隐身术。。你了解集体隐身术吗？\ \ 
<不了解， 请对集体隐身术进行一下说明。/@mugong_masshiding3>

[@mugong_masshiding3]
#IF
#SAY
你知道隐身术是隐藏行踪的魔法吧？\ 
集体隐身术和隐身术相同的是可以隐藏自己的动静，不同的是 {FCOLOR/10}集体隐身术可以隐藏包括你同事动静的魔法{FCOLOR/12} 。\ 
如果掌握该魔法，你就可以给其他人更多的帮助。要学习集体隐身术吗？\ \ 
<是的，要学习。/@mugong_masshiding5> \
<不，下次机会吧.../@mugong_masshiding6_1>

[@mugong_masshiding4]
#IF
#SAY
这样的。那么你可以对叫成致的人为什么行踪不明进行调查吗？\ \ 
<好的，我要试一试。/@mugong_masshiding6_2> \ 
<现在还有其它的事情。/@mugong_masshiding6_1>

[@mugong_masshiding5]
#IF
#SAY
那么，在学习集体隐身术之前先测试你是否有学习集体隐身术的资格。。\ \ 
<好的，我将试一试。/@mugong_masshiding6_2> \
<我现在还没有做好心理准备。/@mugong_masshiding6_1>

[@mugong_masshiding6_1]
#IF
#SAY
知道了。下次准备好了，再来！\ \
<结束/@exit>

[@mugong_masshiding6_2]
#IF
#ACT
set [046] 1
#SAY
真不愧是特殊的年轻人，成致失踪的地点是 {FCOLOR/10}沃玛神殿的2层{FCOLOR/12} 。\
希望到那个地方找到他为什么失踪的 {FCOLOR/10}头绪{FCOLOR/12} 。\ \ 
<好的，知道了。/@exit>
}


[@mugong_masshiding_complete]
{
#IF
check [020] 1
#ACT
break

#IF
check [046] 0
#ACT
break

#IF
check [047] 0
#ACT
break

#IF
check [046] 1
check [047] 1
check [020] 0
checkitem 成致日志 1
#ACT
goto @mugong_masshiding_complete1
#ELSESAY
这个人很辛苦找到的东西丢失在哪儿了？\ 
快点拿去吧！\ \ 
<结束/@exit>

[@mugong_masshiding_complete1]
#IF
checkitem 成致日志
#ACT
take 成致日志 1
#SAY
哦，终于找到了 {FCOLOR/10}日志{FCOLOR/12} 。用这个东西就可以发现道士失踪的原因哦。\
(拿走日志后，看写的文章...)
嗯...\
这个。因此他虽然将集体隐身术的武功书握在手里，却没有完全掌握的样子。\ \ 
<是集体隐身术的要诀？/@mugong_masshiding_complete2>

[@mugong_masshiding_complete2]
#IF
#SAY
是这样的。日志中包含有 {FCOLOR/10}集体隐身术的要诀{FCOLOR/12} 。\ 
但是凭借这种错误分析的要诀如何可以学习到正宗的魔法。\
看来成致实施了集体隐身术，却失败了。在只有隐藏自身的状态下，走火入魔而失去了生命哟。\ 
珍贵的生命就这样消失了。。。\ 
魔法就是这样可怕的哟。自己没有做好也有可能失去生命，那你以后还想继续学习魔法吗？\ 
<是的，我以后将继续学习魔法。/@mugong_masshiding_complete3>

[@mugong_masshiding_complete3]
#IF
check [020] 0
#ACT
give 集体隐身术（秘籍）
give 暗黑竹笛 1
give 金币 21000
reset [045] 2
set [020] 1
#SAY
知道了。你的决心非常大嘛。\ 
你已经在其它地方得到了武功秘籍，我也没有再给你的必要了。我给你一些金币和东西，用在需要的地方。\
我将向小贩解释。\ \ 
<谢谢！/@exit>
}

[@mugong_masshiding_explain]
{
#IF
#SAY
如果想学习集体隐身术，首先从商人那儿听取 {FCOLOR/10}对某个男子的传闻{FCOLOR/12} ，然后来找我。\
我将拜托你到沃玛神殿2层找到那个人的 {FCOLOR/10}痕迹{FCOLOR/12} ，发现这个东西即可。\
称为痕迹的东西有可能就是他的 {FCOLOR/10}日志{FCOLOR/12} 。他的日志被那个地方的怪兽拿着的可能性很高。\ \ 
<结束/@exit>
}
